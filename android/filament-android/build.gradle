// This script accepts the following parameters:
//
// filament_dist_dir
//     Path to the Filament distribution/install directory for Android
//     (produced by make/ninja install). This directory must contain lib/arm64-v8a/ etc.
//
// Example:
//     ./gradlew -Pfilament_dist_dir=../../dist-android-release assembleRelease

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

group = "com.google.android.filament"
version = "1.3"

apply plugin: 'com.android.library'

def filament_path = file("../../out/android-release/filament").absolutePath
if (project.hasProperty("filament_dist_dir")) {
    filament_path = file("$filament_dist_dir").absolutePath
}

android {
    compileSdkVersion 29
    defaultConfig {
        // Our minSdkVersion is actually 19, we lie and say 14 here so apps don't have
        // to increase their minSdkVersion unnecessarily. It is however up to them to
        // ensure they do not initialize Filament on API levels < 19.
        minSdkVersion 14
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        externalNativeBuild {
            cmake {
                arguments.add("-DANDROID_PIE=ON")
                arguments.add("-DANDROID_PLATFORM=android-19")
                arguments.add("-DANDROID_STL=c++_static")
                arguments.add("-DFILAMENT_DIST_DIR=${filament_path}".toString())
                cppFlags.add("-std=c++17")
                if (project.hasProperty('extra_cmake_args')) {
                    arguments.add(extra_cmake_args)
                }
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jni.srcDirs "src/main/cpp"
            // To enable validation layers with the Vulkan backend, uncomment the following lines
            // to copy the appropriate files from the NDK to the device. Also be sure to use a debug
            // configuration for the native build.
            // jniLibs {
            //     srcDirs = ["${android.ndkDirectory}/sources/third_party/vulkan/src/build-android/jniLibs"]
            // }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.android.support:support-annotations:28.0.0'
}
