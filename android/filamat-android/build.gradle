android {
    namespace 'com.google.android.filament.filamat'

    flavorDimensions "functionality"
    productFlavors {
        full {
            dimension "functionality"
        }

        lite {
            dimension "functionality"

            externalNativeBuild {
                cmake {
                    arguments.add("-DFILAMAT_LITE=ON")
                }
            }
        }
    }

    publishing {
        singleVariant("fullRelease") {
            withSourcesJar()
            withJavadocJar()
        }
        singleVariant("liteRelease") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation deps.androidx.annotations
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

afterEvaluate { project ->
    publishing {
        publications {
            fullRelease(MavenPublication) {
                artifactId = POM_ARTIFACT_ID_FULL
                from components.fullRelease
            }

            liteRelease(MavenPublication) {
                artifactId = POM_ARTIFACT_ID_LITE
                from components.liteRelease
            }
        }
    }
}
