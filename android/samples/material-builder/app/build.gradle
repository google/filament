apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: '../../../gradle/filament-tasks.gradle'

compileMesh {
    group 'Filament'
    description 'Compile mesh'

    inputFile = file("../../../../third_party/models/shader_ball/shader_ball.obj")
    outputDir = file("src/main/assets/models")
}

generateIbl {
    group 'Filament'
    description 'Generate IBL'

    inputFile = file("../../../../third_party/environments/flower_road_no_sun_2k.hdr")
    outputDir = file("src/main/assets/envs")
}

preBuild.dependsOn compileMesh
preBuild.dependsOn generateIbl

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.google.android.filament.material_builder"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // Filament comes with native code, the following declarations
    // can be used to generate architecture specific APKs
    flavorDimensions 'cpuArch'
    productFlavors {
        arm8 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'arm64-v8a'
            }
        }
        arm7 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }
        x86_64 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'x86_64'
            }
        }
        x86 {
            dimension 'cpuArch'
            ndk {
                abiFilters 'x86'
            }
        }
        universal {
            dimension 'cpuArch'
        }
    }

    // The filamat library has two variants: full and lite. Here we default to the "full" variant.
    // Replace "full" with "lite" to use the filamat-lite variant, which has a smaller binary size
    // but comes with certain restrictions. See "Filamat Lite" in libs/filamat/README.md.
    defaultConfig {
        missingDimensionStrategy 'functionality', 'full'
    }

    // We use the .filamat extension for materials compiled with matc
    // Telling aapt to not compress them allows to load them efficiently
    aaptOptions {
        noCompress 'filamat'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Depend on Filament
    implementation 'com.google.android.filament:filament-android'

    // Depend on Filamat
    implementation 'com.google.android.filament:filamat-android'
}
