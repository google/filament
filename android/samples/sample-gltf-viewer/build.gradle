plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'filament-tools-plugin'
}

project.ext.isSample = true

filamentTools {
    cmgenArgs = "-q --format=ktx --size=256 --extract-blur=0.1 --deploy=src/main/assets/envs/default_env"
    iblInputFile = project.layout.projectDirectory.file("../../../third_party/environments/lightroom_14b.hdr")
    iblOutputDir = project.layout.projectDirectory.dir("src/main/assets/envs")
}

// don't forget to update MainACtivity.kt when/if changing this.
task copyMesh(type: Copy) {
    from "../../../third_party/models/BusterDrone"
    into "src/main/assets/models"
}

preBuild.dependsOn copyMesh

clean.doFirst {
    delete "src/main/assets"
}

android {
    namespace 'com.google.android.filament.gltf'

    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.google.android.filament.gltf"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    // NOTE: This is a workaround required because the AGP task collectReleaseDependencies
    //       is not configuration-cache friendly yet; this is only useful for Play publication
    dependenciesInfo {
        includeInApk = false
    }

    compileOptions {
        sourceCompatibility versions.jdk
        targetCompatibility versions.jdk
    }
}

dependencies {
    implementation deps.kotlin
    implementation deps.coroutines.core
    implementation project(':filament-android')
    implementation project(':gltfio-android')
    implementation project(':filament-utils-android')
}
