apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: FilamentToolsPlugin

project.ext.isSample = true

filamentTools {
    cmgenArgs = "-q --format=ktx --size=256 --extract-blur=0.1 --deploy=src/main/assets/envs/default_env"
    iblInputFile = project.layout.projectDirectory.file("../../../third_party/environments/lightroom_14b.hdr")
    iblOutputDir = project.layout.projectDirectory.dir("src/main/assets/envs")
}

task copyMesh(type: Copy) {
    from "../../../third_party/models/BusterDrone"
    into "src/main/assets/models"
}

preBuild.dependsOn copyMesh

clean.doFirst {
    delete "src/main/assets"
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.google.android.filament.gltf"
        minSdkVersion 21
        targetSdkVersion versions.targetSdk
        missingDimensionStrategy 'functionality', 'full'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

}

dependencies {
    implementation deps.kotlin
    implementation project(':filament-android')
    implementation project(':gltfio-android')
    implementation project(':filament-utils-android')
    implementation 'androidx.appcompat:appcompat:1.2.0'

    implementation "androidx.camera:camera-camera2:1.0.0-beta04"
    implementation "androidx.camera:camera-lifecycle:1.0.0-beta04"
    implementation "androidx.camera:camera-view:1.0.0-alpha11"

    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'

    implementation 'com.google.mlkit:barcode-scanning:16.1.1'
}
