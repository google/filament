apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: '../../gradle/filament-tasks.gradle'

compileMaterials {
    group 'Filament'
    description 'Compile materials'

    inputDir = file("src/main/materials")
    outputDir = file("src/main/assets/materials")
}

preBuild.dependsOn compileMaterials

clean.doFirst {
    delete "src/main/assets"
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.google.android.filament.textureview"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    // We use the .filamat extension for materials compiled with matc
    // Telling aapt to not compress them allows to load them efficiently
    aaptOptions {
        noCompress 'filamat', 'ktx'
    }
}

dependencies {
    implementation deps.kotlin
    implementation project(':filament-android')
}
