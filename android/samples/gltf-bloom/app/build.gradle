apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: '../../../gradle/filament-tasks.gradle'

compileMaterials {
    group 'Filament'
    description 'Compile materials'

    inputDir = file("src/main/materials")
    outputDir = file("src/main/assets/materials")
}

task copyMesh(type: Copy) {
    from file("../../../../third_party/models/lucy/lucy.glb")
    into file("src/main/assets/models")
}

generateIbl {
    group 'Filament'
    description 'Generate IBL'

    cmgenArgs = "--format=ktx --size=256 --extract-blur=0.1 --deploy=src/main/assets/envs/venetian_crossroads_2k"
    inputFile = file("../../../../third_party/environments/venetian_crossroads_2k.hdr")
    outputDir = file("src/main/assets/envs")
}

preBuild.dependsOn compileMaterials
preBuild.dependsOn copyMesh
preBuild.dependsOn generateIbl

clean.doFirst {
    delete "src/main/assets"
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.google.android.filament.gltf"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    // We use the .filamat extension for materials compiled with matc
    // Telling aapt to not compress them allows to load them efficiently
    aaptOptions {
        noCompress 'filamat', 'ktx'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // Depend on Filament
    implementation 'com.google.android.filament:filament-android'
    implementation 'com.google.android.filament:gltfio-android'
}
