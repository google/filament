apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: FilamentToolsPlugin

filamentTools {
    materialInputDir = file("src/main/materials")
    materialOutputDir = file("src/main/assets/materials")

    cmgenArgs = "-q --format=ktx --size=256 --extract-blur=0.1 --deploy=src/main/assets/envs/venetian_crossroads_2k"
    iblInputFile = file("../../../third_party/environments/venetian_crossroads_2k.hdr")
    iblOutputDir = file("src/main/assets/envs")
}

task copyMesh(type: Copy) {
    from file("../../../third_party/models/lucy/lucy.glb")
    into file("src/main/assets/models")
}

preBuild.dependsOn copyMesh

clean.doFirst {
    delete "src/main/assets"
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.google.android.filament.bloom"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
    }

    // We use the .filamat extension for materials compiled with matc
    // Telling aapt to not compress them allows to load them efficiently
    aaptOptions {
        noCompress 'filamat', 'ktx'
    }
}

dependencies {
    implementation deps.kotlin
    implementation project(':filament-android')
    implementation project(':gltfio-android')
    implementation project(':filament-utils-android')
}
