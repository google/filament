android {
    namespace 'com.google.android.filament.gltfio'

    packagingOptions {
        // No need to package up the following shared libs, which arise as a side effect of our
        // externalNativeBuild dependencies. When clients pick and choose from project-level gradle
        // dependencies, these shared libs already get pulled in, so we need to avoid the error:
        // "More than one file was found with OS independent path ..."
        jniLibs {
            excludes += ['lib/*/libfilament-jni.so']
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

dependencies {
    implementation deps.androidx.annotations

    api project(':filament-android')
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

afterEvaluate { project ->
    publishing {
        publications {
            release(MavenPublication) {
                artifactId = POM_ARTIFACT_ID_FULL
                from components.release
            }
        }
    }
}
