material {
    name : dofTiles,
    parameters : [
        {
            type : sampler2d,
            name : cocMinMax,
            precision: medium
        },
        {
            type : float2,
            name : texelSize,
            precision: high
        }
    ],
    variables : [
        vertex
    ],
    domain : postprocess,
    depthWrite : false,
    depthCulling : false
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        // see explanation in dofDownSample.mat
        postProcess.normalizedUV = uvToRenderTargetUV(postProcess.normalizedUV);
        postProcess.vertex.xy = postProcess.normalizedUV - 0.5 * materialParams.texelSize;
    }
}

fragment {

void dummy(){}

float max4(const vec4 f) {
    vec2 t = max(f.xy, f.zw);
    return max(t.x, t.y);
}

float min4(const vec4 f) {
    vec2 t = min(f.xy, f.zw);
    return min(t.x, t.y);
}

void postProcess(inout PostProcessInputs postProcess) {
    highp vec2 uv = variable_vertex.xy;
    highp vec2 size = vec2(textureSize(materialParams_cocMinMax, 0));

    ivec2 i = ivec2(uv * size - 0.5);

    // note: the first time, the source texture is swizzled -- so .r and .g are the same channel
    // if swizzling is not supported, we use dofTilesSwizzle instead.
#if defined(FILAMENT_HAS_FEATURE_TEXTURE_GATHER)
    vec4 mi = textureGather(materialParams_cocMinMax, uv, 0); // 01, 11, 10, 00
    vec4 ma = textureGather(materialParams_cocMinMax, uv, 1); // 01, 11, 10, 00
#else
    vec2 s01 = texelFetchOffset(materialParams_cocMinMax, i, 0, ivec2(0, 1)).rg;
    vec2 s11 = texelFetchOffset(materialParams_cocMinMax, i, 0, ivec2(1, 1)).rg;
    vec2 s10 = texelFetchOffset(materialParams_cocMinMax, i, 0, ivec2(1, 0)).rg;
    vec2 s00 = texelFetchOffset(materialParams_cocMinMax, i, 0, ivec2(0, 0)).rg;
    vec4 mi = vec4(s01.r, s11.r, s10.r, s00.r);
    vec4 ma = vec4(s01.g, s11.g, s10.g, s00.g);
#endif

    // compute tile's min CoC
    postProcess.color.r = min4(mi);

    // compute tile's max CoC
    postProcess.color.g = max4(ma);
}

}
