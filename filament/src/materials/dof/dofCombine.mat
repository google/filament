material {
    name : dofCombine,
    parameters : [
        {
            type : sampler2d,
            name : color,
            precision: medium
        },
        {
            type : sampler2d,
            name : dof,
            precision: medium
        },
        {
            type : sampler2d,
            name : tiles,
            precision: medium
        },
        {
            type : sampler2d,
            name : alpha,
            precision: medium
        },
        {
            type : float4,
            name : uvscale,
            precision: high
        }
    ],
    variables : [
        vertex
    ],
    domain : postprocess,
    depthWrite : false,
    depthCulling : false
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        postProcess.vertex.xy = postProcess.normalizedUV;
        postProcess.vertex.zw = postProcess.normalizedUV * materialParams.uvscale.zw;
    }
}

fragment {

#include "dofUtils.fs"

void dummy(){}

void postProcess(inout PostProcessInputs postProcess) {
    highp vec2 uv2 = variable_vertex.zw;
    vec2 tiles = textureLod(materialParams_tiles, uv2, 0.0).rg;

    vec4 color;
    if (isTrivialTile(tiles)) {
        highp vec2 uv0 = variable_vertex.xy;
        color = textureLod(materialParams_color, uv0, 0.0);
    } else if (isFastTile(tiles) && (abs(tiles.r) >= MAX_IN_FOCUS_COC + 1.0)) {
        // we can only handle fast tiles that don't have an alpha channel here
        highp vec2 uv1 = variable_vertex.xy * materialParams.uvscale.xy;
        color = textureLod(materialParams_dof, uv1, 0.0);
    } else {
        highp vec2 uv0 = variable_vertex.xy;
        highp vec2 uv1 = variable_vertex.xy * materialParams.uvscale.xy;
        float alpha = textureLod(materialParams_alpha, uv1, 0.0).r;
        vec4 foreground = textureLod(materialParams_dof, uv1, 0.0);
        color = textureLod(materialParams_color, uv0, 0.0);
        color = foreground + (1.0 - alpha) * color;
    }

#if POST_PROCESS_OPAQUE
    // kill the work performed above
    color.a = 1.0;
#endif

    postProcess.color = color;
}

}
