material {
    name : DepthOfFieldMipmap,
    parameters : [
        {
            type : sampler2d,
            name : foreground,
            precision: medium
        },
        {
            type : sampler2d,
            name : background,
            precision: medium
        },
        {
            type : sampler2d,
            name : cocFgBg,
            precision: medium
        },
        {
            type : int,
            name : mip
        },
        {
            type : float,
            name : weightScale
        }
    ],
    variables : [
        vertex
    ],
    outputs : [
        {
            name : color,
            target : color,
            type : float4
        },
        {
            name : background,
            target : color,
            type : float4
        },
        {
            name : cocFgBg,
            target : color,
            type : float2
        }
    ],
    domain : postprocess,
    depthWrite : false,
    depthCulling : false
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        postProcess.vertex.xy = postProcess.normalizedUV;
    }
}

fragment {

#include "dofUtils.fs"

void dummy(){}

void postProcess(inout PostProcessInputs postProcess) {
    highp vec2 pixelSize = 1.0 / vec2(textureSize(materialParams_foreground, materialParams.mip));
    highp vec2 adjusted_uv = variable_vertex.xy - pixelSize.xy * 0.5;
    highp vec4 uv = vec4(adjusted_uv + pixelSize, adjusted_uv);

    // the source is guaranteed to be a multiple of two, so we now the bilinear weights are 0.25

    // the bilateral weights need to be scaled by to match the lower resolution
    float weightScale = materialParams.weightScale;
    float mip = float(materialParams.mip);

    // fetch the 4 corresponding CoC
    vec4 d0, d1;
    d0.xy = textureLod(materialParams_cocFgBg, uv.zy, mip).rg;
    d0.zw = textureLod(materialParams_cocFgBg, uv.xy, mip).rg;
    d1.xy = textureLod(materialParams_cocFgBg, uv.xw, mip).rg;
    d1.zw = textureLod(materialParams_cocFgBg, uv.zw, mip).rg;
    vec4 fw = vec4(d0.xz, d1.xz);
    vec4 bw = vec4(d0.yw, d1.yw);

    vec4 hdr;

    /*
     * foreground
     */

    vec4 fg01 = textureLod(materialParams_foreground, uv.zy, mip);
    vec4 fg11 = textureLod(materialParams_foreground, uv.xy, mip);
    vec4 fg10 = textureLod(materialParams_foreground, uv.xw, mip);
    vec4 fg00 = textureLod(materialParams_foreground, uv.zw, mip);

    // fireflies/flickering filtering
    hdr.x = 1.0 / (1.0 + max4(fg00));
    hdr.y = 1.0 / (1.0 + max4(fg11));
    hdr.z = 1.0 / (1.0 + max4(fg10));
    hdr.w = 1.0 / (1.0 + max4(fg00));

    // We calculates the downsampled CoC to be conservative, that is if any
    // of the 4 texels are foreground or background, then the whole new texel is too.
    float fgCoc = min4(fw);

    // The forground bilateral weight is calculated as saturate(1.0 - (fgCoc - w)), which always
    // yields 1.0. Note the "missing" abs(), this is because we want to let the background layer
    // leak into the foreground layer, to avoid aliasing artifacts.
    fw = hdr;
    float fgScale = 1.0 / (fw.x + fw.y + fw.z + fw.w);
    vec4 foreground = (fg01 * fw.x + fg11 * fw.y + fg10 * fw.z + fg00 * fw.w) * fgScale;

    /*
     * background
     */

    vec4 bg01 = textureLod(materialParams_background, uv.zy, mip);
    vec4 bg11 = textureLod(materialParams_background, uv.xy, mip);
    vec4 bg10 = textureLod(materialParams_background, uv.xw, mip);
    vec4 bg00 = textureLod(materialParams_background, uv.zw, mip);

    // fireflies/flickering filtering
    hdr.x = 1.0 / (1.0 + max4(bg00));
    hdr.y = 1.0 / (1.0 + max4(bg11));
    hdr.z = 1.0 / (1.0 + max4(bg10));
    hdr.w = 1.0 / (1.0 + max4(bg00));

    float bgCoc = max4(bw);

    // The background bilateral weight is calculated as saturate(1.0 - abs(bgCoc - w)),
    // but the abs() is not needed since bgCoc - w is guaranteed to be >= 0.
    bw = saturate(1.0 - (bgCoc - bw) * weightScale) * hdr;
    float bgScale = 1.0 / (bw.x + bw.y + bw.z + bw.w);
    vec4 background = (bg01 * bw.x + bg11 * bw.y + bg10 * bw.z + bg00 * bw.w) * bgScale;

#if POST_PROCESS_OPAQUE
    // kill the work performed above
    foreground.a = 1.0;
    background.a = 1.0;
#endif

    // Output to MRTs
    postProcess.color       = foreground;
    postProcess.background  = background;
    postProcess.cocFgBg     = vec2(fgCoc, bgCoc);
}

}
