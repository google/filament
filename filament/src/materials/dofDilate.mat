material {
    name : DepthOfFieldDilate,
    parameters : [
        {
            type : sampler2d,
            name : tiles,
            precision: medium
        }
    ],
    variables : [
        vertex
    ],
    domain : postprocess,
    depthWrite : false,
    depthCulling : false
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        postProcess.vertex.xy = postProcess.normalizedUV;
    }
}

fragment {

#include "dofUtils.fs"

void dummy(){}

// Tiles of 16 pixels requires two dilate rounds to accomodate our max Coc of 32 pixels
#define TILE_SIZE   16.0

vec2 tap(const vec2 uv, const vec2 offset) {
    return textureLod(materialParams_tiles, uv + offset, 0.0).rg;
}

vec2 dilate(inout vec2 center, const vec2 tap) {
    // 2.24 (sqrt(5) is the longest distance between 2 pixels of adjacent tiles).
    const float maxDistance = max(-MAX_COC_RADIUS, -2.24 * TILE_SIZE);

    // Tiles that can affect us need to transfer their CoC to us (for the foreground it's
    // min(ourCoc, theirCoc) because CoC are negative (the min is the larger radius).

    // TODO: However, if their CoC is very large, we can limit it by the distance to that
    // tile, because we only need to reach *them*).
    // The difficuty here is the this clamping depends on if we're negative or positive.
    // e.g. for positive values
    //    center.g = min(center.g, max(tap.g, maxDistance));
    //    center.r = max(center.r, max(tap.r, maxDistance));

    center.g = min(center.g, tap.g);
    center.r = max(center.r, tap.r);
    return center;
}

void postProcess(inout PostProcessInputs postProcess) {
    const float radius = 1.0;
    // we need highp here just to maintain precision of 1/x
    highp vec2 size = vec2(textureSize(materialParams_tiles, 0));
    highp vec2 isize = 1.0 / size;

    vec2 radii = float(radius) * isize;

    vec2 uv = variable_vertex.xy;

    // center
    vec2 center = tap(uv, vec2(0, 0));

    // horizontal sides
    for (float j = -radius ; j <= radius ; j += 1.0) {
        float y = j * isize.y;
        dilate(center, tap(uv, vec2(-radii.x, y)));
        dilate(center, tap(uv, vec2( radii.x, y)));
    }

    // vertical sides
    for (float i = -radius + 1.0 ; i <= radius - 1.0 ; i += 1.0) {
        float x = i * isize.x;
        dilate(center, tap(uv, vec2(x, -radii.y)));
        dilate(center, tap(uv, vec2(x,  radii.y)));
    }

    postProcess.color.rg = center;
}
}
