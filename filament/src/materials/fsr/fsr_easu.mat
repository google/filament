material {
    name : fsr_easu,
    parameters : [
        {
            type : sampler2d,
            name : color,
            precision: medium
        },
        {
            type : sampler2d,
            name : colorNearest,
            precision: medium
        },
        {
            type : float2,
            name : colorHalfTexelOffset,
            precision: high
        },
        {
            type : float4,
            name : resolution,
            precision: high
        },
        {
            type : float4,
            name : EasuCon0,
            precision: high
        },
        {
            type : float4,
            name : EasuCon1,
            precision: high
        },
        {
            type : float4,
            name : EasuCon2,
            precision: high
        },
        {
            type : float4,
            name : EasuCon3,
            precision: high
        }
    ],
    variables : [
        vertex
    ],
    depthWrite : false,
    depthCulling : false,
    domain: postprocess
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        postProcess.vertex.xy = postProcess.normalizedUV * materialParams.resolution.xy;
    }
}

fragment {

    precision mediump float;
    precision highp int;

    #define A_GPU 1
    #define A_GLSL 1
    #include "ffx_a.h"
    #define FSR_EASU_F 1
    #include "ffx_fsr1.h"

#if defined(FILAMENT_HAS_FEATURE_TEXTURE_GATHER)
    #define gather textureGather
#else
    vec4 gather(const mediump sampler2D colorNearest, highp vec2 p, const int comp) {
        p -= materialParams.colorHalfTexelOffset;
        vec4 d;
        d[0] = textureLodOffset(colorNearest, p, 0.0, ivec2(0, 1))[comp];
        d[1] = textureLodOffset(colorNearest, p, 0.0, ivec2(1, 1))[comp];
        d[2] = textureLodOffset(colorNearest, p, 0.0, ivec2(1, 0))[comp];
        d[3] = textureLodOffset(colorNearest, p, 0.0, ivec2(0, 0))[comp];
        return d;
    }
#endif

    AF4 FsrEasuRF(AF2 p) {
        return gather(materialParams_colorNearest, p, 0);
    }

    AF4 FsrEasuGF(AF2 p) {
        return gather(materialParams_colorNearest, p, 1);
    }

    AF4 FsrEasuBF(AF2 p) {
        return gather(materialParams_colorNearest, p, 2);
    }

    void postProcess(inout PostProcessInputs postProcess) {
        highp vec2 p = floor(variable_vertex.xy);

        AF3 pix;
        FsrEasuF(pix, p,
            materialParams.EasuCon0,
            materialParams.EasuCon1,
            materialParams.EasuCon2,
            materialParams.EasuCon3);

        postProcess.color.rgb = pix;
        postProcess.color.a = 1.0; // TODO: FSR doesn't support transparency.
    }
}
