material {
    name : Skybox,
    parameters : [
        {
           type : bool,
           name : showSun
        },
        {
           type : samplerCubemap,
           name : skybox
        }
    ],
    variables : [
         eyeDirection
    ],
    vertexDomain : device,
    depthWrite : false,
    shadingModel : unlit,
    variantFilter : [ skinning, shadowReceiver ],
    culling: none
}

fragment {
    void material(inout MaterialInputs material) {
        prepareMaterial(material);
        vec3 sky = texture(materialParams_skybox, variable_eyeDirection.xyz).rgb;
        sky *= frameUniforms.iblLuminance;
        if (materialParams.showSun && frameUniforms.sun.w >= 0.0f) {
            vec3 direction = normalize(variable_eyeDirection.xyz);
            // Assume the sun is a sphere
            vec3 sun = frameUniforms.lightColorIntensity.rgb *
                    (frameUniforms.lightColorIntensity.a * (4.0 * PI));
            float cosAngle = dot(direction, frameUniforms.lightDirection);
            float x = (cosAngle - frameUniforms.sun.x) * frameUniforms.sun.z;
            float gradient = pow(1.0 - saturate(x), frameUniforms.sun.w);
            sky = sky + gradient * sun;
        }
        material.baseColor = vec4(sky, 1.0);
    }
}

vertex {
    void materialVertex(inout MaterialVertexInputs material) {
        float3 p = getPosition().xyz;
        float3 unprojected = mulMat4x4Float3(getViewFromClipMatrix(), p).xyz;
        material.eyeDirection.xyz = mulMat3x3Float3(getWorldFromViewMatrix(), unprojected);
    }
}
