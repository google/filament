material {
    name : Skybox,
    parameters : [
        {
           type : int,
           name : showSun
        },
        {
           type : int,
           name : constantColor
        },
        {
           type : samplerCubemap,
           name : skybox
        },
        {
           type : float4,
           name : color
        }
    ],
    variables : [
         eyeDirection
    ],
    vertexDomain : device,
    depthWrite : false,
    shadingModel : unlit,
    variantFilter : [ skinning, shadowReceiver ],
    culling: none
}

fragment {
    void material(inout MaterialInputs material) {
        prepareMaterial(material);
        vec4 sky;
        if (materialParams.constantColor != 0) {
            sky = materialParams.color;
        } else {
            sky = vec4(texture(materialParams_skybox, variable_eyeDirection.xyz).rgb, 1.0);
            sky.rgb *= frameUniforms.iblLuminance;
        }
        if (materialParams.showSun != 0 && frameUniforms.sun.w >= 0.0f) {
            vec3 direction = normalize(variable_eyeDirection.xyz);
            // Assume the sun is a sphere
            vec3 sun = frameUniforms.lightColorIntensity.rgb *
                    (frameUniforms.lightColorIntensity.a * (4.0 * PI));
            float cosAngle = dot(direction, frameUniforms.lightDirection);
            float x = (cosAngle - frameUniforms.sun.x) * frameUniforms.sun.z;
            float gradient = pow(1.0 - saturate(x), frameUniforms.sun.w);
            sky.rgb = sky.rgb + gradient * sun;
        }
        material.baseColor = sky;
    }
}

vertex {
    void materialVertex(inout MaterialVertexInputs material) {
        float3 p = getPosition().xyz;
        float3 unprojected = mulMat4x4Float3(getViewFromClipMatrix(), p).xyz;
        material.eyeDirection.xyz = mulMat3x3Float3(getWorldFromViewMatrix(), unprojected);
    }
}
