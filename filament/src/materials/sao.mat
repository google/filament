material {
    name : sao,
    parameters : [
        {
            type : sampler2d,
            name : depth,
            precision: high
        },
        {
            type : float4,
            name : resolution
        },
        {
            type : float,
            name : radius
        },
        {
            type : float,
            name : invRadiusSquared
        },
        {
            type : float,
            name : projectionScaleRadius
        },
        {
            type : float,
            name : bias
        },
        {
            type : float,
            name : power
        },
        {
            type : int,
            name : maxLevel
        }
    ],
    variables : [
         vertex
    ],
    domain : postprocess,
    depthWrite : false,
    depthCulling : true
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        postProcess.vertex.xy = postProcess.normalizedUV;
    }
}

fragment {
    #define IMPLEMENTATION_REF          0
    #define IMPLEMENTATION_REF_TAP      1
    #define IMPLEMENTATION_LUT_TAP      2
    #define IMPLEMENTATION              IMPLEMENTATION_LUT_TAP

    #define LOG2_LOD_RATE   3

    #define NOISE_NONE      0
    #define NOISE_PATTERN   1
    #define NOISE_RANDOM    2
    #define NOISE_TYPE      NOISE_PATTERN

    const float kSpiralTurns = 7.0; // must match ssaogen.cpp
    const uint kSpiralSampleCount = 7u;
    const vec3 kSpiralSamples[] = vec3[](
        vec3(0.966326, 0.257322, 0.0769231), vec3(-0.69718, 0.716896, 0.230769),
        vec3(0.975223, 0.221226, 0.384615), vec3(0.982771, 0.184826, 0.538462),
        vec3(-0.613055, 0.79004, 0.692308), vec3(0.997237, 0.0742889, 0.846154),
        vec3(1, -1.16027e-06, 1)
    );

    const uint kTrigNoiseSampleCount = 16u;
    const vec3 kTrigNoiseSamples[] = vec3[](
        vec3(-0.401209, 0.915987, 0.0317202), vec3(0.843133, -0.537704, 0.0879207),
        vec3(0.457543, 0.889188, 0.0176937), vec3(0.976762, -0.214325, 0.0930107),
        vec3(-0.0596454, 0.99822, 0.026175), vec3(0.704176, 0.710025, 0.107385),
        vec3(-0.707961, 0.706251, 0.0375434), vec3(0.909426, 0.415866, 0.102259),
        vec3(0.92462, 0.38089, 0.00636207), vec3(-0.0938624, 0.995585, 0.0267157),
        vec3(-0.905196, -0.424993, 0.056284), vec3(0.196104, 0.980583, 0.115615),
        vec3(-0.752002, -0.659161, 0.0605199), vec3(-0.575921, 0.817505, 0.12691),
        vec3(0.794595, -0.60714, 0.0866893), vec3(0.178844, 0.983877, 0.115861)
    );

    vec2 sq(const vec2 a) {
        return a * a;
    }

    // remaps to -1 and 1, repeating
    float reduce(highp float x) {
        return fract(0.5 * x + 0.5) * 2.0 - 1.0;
    }

    // very crude and fast sin/cos approximation
    vec2 fast_cossin(highp float x) {
        x *= 1.0/3.1415926;
        vec2 a = vec2(reduce(x + 0.5), reduce(x));
        vec2 xn = sq(a * 2.0 + 1.0) - 1.0;
        vec2 xp = 1.0 - sq(a * 2.0 - 1.0);
        return vec2(a.x < 0.0 ? xn.x : xp.x, a.y < 0.0 ? xn.y : xp.y);
    }

    // random number between 0 and 1
    float random(highp vec2 n) {
        n  = fract(n * vec2(5.3987, 5.4421));
        n += dot(n.yx, n.xy + vec2(21.5351, 14.3137));
        highp float xy = n.x * n.y;
        // compute in [0..2[ and remap to [0.0..1.0[
        return fract(xy * 95.4307) + fract(xy * 75.04961) * 0.5;
    }

    // see ssaogen.cpp
    vec3 getTrigNoise(const vec2 uv) {
        #if NOISE_TYPE == NOISE_RANDOM
            const float dalpha = 1.0f / (float(kSpiralSampleCount) - 0.5f);
            float phi = random(uv);
            float dr = phi * dalpha;
            highp float dalpha2 = sq(dalpha);
            highp float dphi =(6.28318 * kSpiralTurns) * sq(phi) * dalpha2
                + phi * 6.28318 * (1.0 + kSpiralTurns * dalpha2)
                + phi * (2.0 * 6.28318 * kSpiralTurns) * dalpha2;
            return vec3(fast_cossin(dphi), dr);
        #elif NOISE_TYPE == NOISE_PATTERN
            uint ix = uint(gl_FragCoord.x) & 3u;
            uint iy = uint(gl_FragCoord.y) & 3u;
            return kTrigNoiseSamples[ix + iy * 4u];
        #else
            return vec3(0.0);
        #endif
    }

    highp float linearizeDepth(highp float depth) {
        // Our far plane is at infinity, which causes a division by zero below, which in turn
        // causes some issues on some GPU. We workaround it by replacing "infinity" by the closest
        // value representable in  a 24 bit depth buffer.
        const float preventDiv0 = -1.0 / 16777216.0;
        highp mat4 projection = getClipFromViewMatrix();
        highp float z = depth * 2.0 - 1.0; // depth in clip space
        return -projection[3].z / min(preventDiv0, z + projection[2].z);
    }

    highp float sampleDepthLinear(const vec2 uv) {
        return linearizeDepth(texture(materialParams_depth, uv, 0.0).r);
    }

    highp vec3 computeViewSpacePositionFromDepth(vec2 p, highp float linearDepth) {
        p = p * 2.0 - 1.0; // to clip space
        highp mat4 invProjection = getViewFromClipMatrix();
        p.x *= invProjection[0].x;
        p.y *= invProjection[1].y;
        return vec3(p * -linearDepth, linearDepth);
    }

    // compute normals using derivatives, which essentially results in half-resolution normals
    // this creates arifacts around geometry edges
    highp vec3 computeViewSpaceNormalNotNormalized(const highp vec3 position) {
        highp vec3 dpdx = dFdx(position);
        highp vec3 dpdy = dFdy(position);
        return cross(dpdx, dpdy);
    }

    // compute normals directly from the depth texture, resulting in full resolution normals
    highp vec3 computeViewSpaceNormalNotNormalized(const highp vec3 position, const vec2 uv) {
        vec2 uvdx = uv + vec2(materialParams.resolution.z, 0.0);
        vec2 uvdy = uv + vec2(0.0, materialParams.resolution.w);
        highp vec3 px = computeViewSpacePositionFromDepth(uvdx, sampleDepthLinear(uvdx));
        highp vec3 py = computeViewSpacePositionFromDepth(uvdy, sampleDepthLinear(uvdy));
        highp vec3 dpdx = px - position;
        highp vec3 dpdy = py - position;
        return cross(dpdx, dpdy);
    }

    // Ambient Occlusion, largely inspired from:
    // "The Alchemy Screen-Space Ambient Obscurance Algorithm" by Morgan McGuire
    // "Scalable Ambient Obscurance" by Morgan McGuire, Michael Mara and David Luebke

    vec3 tapLocationReference(uint i) {
        const float dalpha = 1.0f / (float(kSpiralSampleCount) - 0.5f);
        float phi = random(variable_vertex.xy);
        float radius = (float(i) + phi + 0.5) * dalpha;
        float angle = (radius * radius) * (kSpiralTurns * 6.28318) + phi * 6.28318;
        return vec3(cos(angle), sin(angle), radius * radius);
    }

    vec3 tapLocationReferenceNoNoise(uint i) {
        const float dalpha = 1.0f / (float(kSpiralSampleCount) - 0.5f);
        float radius = (float(i) + 0.5) * dalpha;
        float angle = (radius * radius) * (kSpiralTurns * 6.28318);
        return vec3(cos(angle), sin(angle), radius);
    }

    vec3 tapLocation(uint i, const vec3 noise) {
        #if IMPLEMENTATION == IMPLEMENTATION_REF
            return tapLocationReference(i);
        #else
            #if IMPLEMENTATION == IMPLEMENTATION_REF_TAP
                vec3 tap = tapLocationReferenceNoNoise(i);
            #elif IMPLEMENTATION == IMPLEMENTATION_LUT_TAP
                vec3 tap = kSpiralSamples[i];
            #endif
            mat2 M = mat2(noise.xy, vec2(-noise.y, noise.x));
            float radius = tap.z + noise.z;
            return vec3(M * tap.xy, radius * radius);
        #endif
    }

    ivec2 clampToEdge(ivec2 uv, int level) {
        ivec2 size = textureSize(materialParams_depth, level);
        return clamp(uv, ivec2(0), size - ivec2(1));
    }

    float computeAmbientOcclusionSAO(uint i, float ssDiskRadius, const ivec2 ssOrigin,
            const highp vec3 origin, const vec3 normal, const vec3 noise) {

        vec3 tap = tapLocation(i, noise);
        float ssRadius = max(1.0, tap.z * ssDiskRadius); // at least 1 pixel screen-space radius

        ivec2 ssSamplePos = ssOrigin + ivec2(ssRadius * tap.xy);
        vec2 uvSamplePos = (vec2(ssSamplePos) + vec2(0.5)) * materialParams.resolution.zw;

        // level = floor(log2(screenSpaceRadius/rate)))
        int level = clamp(int(floor(log2(ssRadius))) - LOG2_LOD_RATE, 0, materialParams.maxLevel);
        highp float depth = texelFetch(materialParams_depth, clampToEdge(ssSamplePos >> level, level), level).r;
        highp float occlusionDepth = linearizeDepth(depth);
        highp vec3 p = computeViewSpacePositionFromDepth(uvSamplePos, occlusionDepth);

        // now we have the sample, compute AO
        vec3 v = p - origin;        // sample vector
        float vv = dot(v, v);       // squared distance
        float vn = dot(v, normal);  // distance * cos(v, normal)
        const float uu = 0.015;
        float falloff = saturate(1.0 - vv * materialParams.invRadiusSquared);
        float occlusion = falloff * max(0.0, vn - materialParams.bias) / (vv + uu);
        return occlusion;
    }

    void postProcess(inout PostProcessInputs postProcess) {
        vec2 uv = variable_vertex.xy; // interpolated to pixel center

        highp float depth = sampleDepthLinear(uv);
        highp vec3 origin = computeViewSpacePositionFromDepth(uv, depth);
        highp vec3 normalNotNormalized = computeViewSpaceNormalNotNormalized(origin, uv);

        vec3 normal = normalize(normalNotNormalized);
        vec3 noise = getTrigNoise(uv);

        // Choose the screen-space sample radius
        // proportional to the projected area of the sphere
        float ssDiskRadius = -(materialParams.projectionScaleRadius / origin.z);
        ivec2 ssOrigin = ivec2(uv * materialParams.resolution.xy);

        float occlusion = 0.0;
        for (uint i = 0u; i < kSpiralSampleCount; i++) {
            occlusion += computeAmbientOcclusionSAO(i, ssDiskRadius, ssOrigin, origin, normal, noise);
        }

        float ao = 1.0 - occlusion / float(kSpiralSampleCount);

        // simulate user-controled ao^n with n[1, 2]
        ao = mix(ao, ao * ao, materialParams.power);

        postProcess.color.r = ao;
    }
}
