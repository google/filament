material {
    name : bloomUpsample,
    parameters : [
        {
            type : sampler2d,
            name : source,
            precision: medium
        },
        {
            type : float4,
            name : resolution,
            precision: high
        },
        {
            type : float,
            name : level
        }
    ],
    variables : [
        vertex
    ],
    domain : postprocess,
    depthWrite : false,
    depthCulling : false
}

vertex {
    void postProcessVertex(inout PostProcessVertexInputs postProcess) {
        postProcess.vertex.xy = postProcess.normalizedUV;
    }
}

fragment {
    void postProcess(inout PostProcessInputs postProcess) {
        float lod = materialParams.level;
        highp vec2 uv = variable_vertex.xy;
        highp float du = 2.0 * materialParams.resolution.z;
        highp float dv = 2.0 * materialParams.resolution.w;

        vec3 c0  = 4.0 * textureLod(materialParams_source, uv, lod).rgb;
        c0 += textureLod(materialParams_source, uv + vec2(-du, -dv), lod).rgb;
        c0 += textureLod(materialParams_source, uv + vec2( du, -dv), lod).rgb;
        c0 += textureLod(materialParams_source, uv + vec2( du,  dv), lod).rgb;
        c0 += textureLod(materialParams_source, uv + vec2(-du,  dv), lod).rgb;

        vec3 c1 = textureLod(materialParams_source, uv + vec2(-du,  0.0), lod).rgb;
           c1  += textureLod(materialParams_source, uv + vec2( 0.0, -dv), lod).rgb;
           c1  += textureLod(materialParams_source, uv + vec2( du,  0.0), lod).rgb;
           c1  += textureLod(materialParams_source, uv + vec2( 0.0,  dv), lod).rgb;

        postProcess.color.rgb = (c0 + 2.0 * c1) * (1.0 / 16.0);
    }
}
